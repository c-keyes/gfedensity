{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": 4,
   "id": "23a68a",
   "metadata": {
    "collapsed": false
   },
   "outputs": [
   ],
   "source": [
    "# Example_332.ipynb\n",
    "#\n",
    "# Handling the special case of (l,m,n) = (3,3,2)\n",
    "#\n",
    "# Author: Christopher Keyes\n",
    "# Updated 29 December 2025\n",
    "\n",
    "load('gfedensity.sage')"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 11,
   "id": "31c09b",
   "metadata": {
    "collapsed": false
   },
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "p = 1 mod 3\n",
      "rho_332(p) = 1 -\n",
      "2*p^11 + 4*p^10 + 14*p^9 + 10*p^8 + 14*p^7 + 8*p^6 + 4*p^5 + 4*p^4 - 8*p^2 - 6*p - 6\n",
      "/\n",
      "3*(p^2 + p + 1)^3*(p^2 - p + 1)*(p^2 + 1)*(p + 1)*p\n",
      "\n",
      "p = 2 mod 3\n",
      "rho_332(p) = 1 -\n",
      "4*p^9 + 2*p^8 + 4*p^7 + 2*p^6 + 2*p^4 - 2*p^2 - 2*p - 2\n",
      "/\n",
      "(p^2 + p + 1)^3*(p^2 - p + 1)*(p^2 + 1)*(p + 1)*p\n",
      "\n",
      "Checking agreement\n",
      "    p = 1 mod 3 (expect 0): 0\n",
      "    p = 2 mod 3 (expect 0): 0\n",
      "\n",
      "p = 3\n",
      "rho_332(3) = 424871/461370\n"
     ]
    }
   ],
   "source": [
    "# (l,m,n) = (3,3,2), p=3 case\n",
    "\n",
    "# set variable for gcd(p-1,3)\n",
    "var('gcd_pmin1_3')\n",
    "\n",
    "# solve_332\n",
    "#     solve for rho_332(p). This essentially duplicates the corresponding function in gfedensity.sage\n",
    "#       in this special case. However, it allows more flexibility and can handle p=3.\n",
    "# INPUTS:\n",
    "#     * isp3, true indicates we're solving for p=3 (default false)\n",
    "# OUTPUTS:\n",
    "#     * r, rA, rB, rC, rAB, rAC, rBC, the usual probabilities\n",
    "#     * r_110_x_val,r_120_x_val,r_101_x_val, auxiliary probabilities rho_1bc^(x)\n",
    "#     * r_101_z_val,r_201_z_val,r_011_z_val,r_021_z_val, auxiliary probabilities rho_ab1^(z)\n",
    "def solve_332(isp3 = false):\n",
    "    # establish easy probabilities\n",
    "    r0 = 1\n",
    "    rA = 1\n",
    "    rB = 1\n",
    "\n",
    "    # establish rhoC and variants\n",
    "    if isp3:\n",
    "        rC_x = 13/18\n",
    "        rC_y = rC_x\n",
    "        rC = 11/13\n",
    "        rC_z = 73/117    \n",
    "        r_001_x = 1\n",
    "    else:\n",
    "        rC_x = 1/gcd_pmin1_3\n",
    "        rC_y = rC_x\n",
    "        rC   = ((1-1/p^2)/gcd_pmin1_3 + (p-1)/p^3)/(1-1/p^3)\n",
    "        rC_z = rC\n",
    "        r_001_x = 1/gcd_pmin1_3\n",
    "\n",
    "    # define auxiliary probabilities\n",
    "    var('r_110_x')\n",
    "    var('r_120_x')\n",
    "    var('r_101_x')\n",
    "    var('r_101_z')\n",
    "    var('r_201_z')\n",
    "    var('r_011_z')\n",
    "    var('r_021_z')\n",
    "    var_list = [r_110_x,r_120_x,r_101_x,r_101_z,r_201_z,r_011_z,r_021_z]\n",
    "    \n",
    "    # build relations\n",
    "    rel_list = [\n",
    "        (r_110_x == (p-1)/p*r_001_x + 1/p*r_101_x),\n",
    "        (r_120_x == (p-1)/p^2 + 1/p^2),\n",
    "        (r_101_x == (p-1)/p + 1/p*r_120_x),\n",
    "        (r_101_z == (p-1)/p + 1/p*r_021_z),\n",
    "        (r_201_z == (p-1)/p^2 + 1/p^2*r_011_z),\n",
    "        (r_011_z == (p-1)/p + 1/p*r_201_z),\n",
    "        (r_021_z == (p-1)/p^2 + 1/p^2*r_101_z)\n",
    "    ]\n",
    "\n",
    "    # solve for auxiliaries\n",
    "    S_aux = solve(rel_list, var_list)\n",
    "    r_110_x_val,r_120_x_val,r_101_x_val,r_101_z_val,r_201_z_val,r_011_z_val,r_021_z_val = [S_aux[0][i].right() for i in [0 .. len(S_aux[0])-1]]\n",
    "    \n",
    "    # compute rho and friends\n",
    "    rAB = ((p-1)^2/p^2*r_001_x + 2*(p-1)/p^2*r_101_x_val + (p-1)^2/p^4 + 2*(p-1)/p^4)/(1-1/p^4)\n",
    "    rBC = ((p-1)^2/p^2 + (p-1)/p^2*(r_201_z_val + r_120_x_val) + (p-1)^2/p^4 + (p-1)/p^4*(r_101_z_val + r_110_x_val) + (p-1)^2/p^6 + (p-1)/p^6*(rC + 1))/(1-1/p^6)\n",
    "    rAC = rBC\n",
    "    r   = ((p-1)^3/p^3 + (p-1)^2/p^3*(rA + rB + rC) + (p-1)/p^3*(rAB + rAC + rBC))/(1-1/p^3)\n",
    "    \n",
    "    return r, rA, rB, rC, rAB, rAC, rBC, r_110_x_val,r_120_x_val,r_101_x_val,r_101_z_val,r_201_z_val,r_011_z_val,r_021_z_val\n",
    "\n",
    "# Double check we are getting the right answer\n",
    "r, rA, rB, rC, rAB, rAC, rBC, r_110_x_val,r_120_x_val,r_101_x_val,r_101_z_val,r_201_z_val,r_011_z_val,r_021_z_val = solve_332()\n",
    "\n",
    "# get known probabilities\n",
    "r_332, rA_332, rB_332, rC_332, rAB_332, rAC_332, rBC_332 = get_probs(3,3,2)\n",
    "\n",
    "# print results\n",
    "print(\"p = 1 mod 3\")\n",
    "print(\"rho_332(p) = 1 -\")\n",
    "print((1-r(gcd_pmin1_3=3)).factor().numerator())\n",
    "print(\"/\")\n",
    "print((1-r(gcd_pmin1_3=3)).factor().denominator()); print()\n",
    "print(\"p = 2 mod 3\")\n",
    "print(\"rho_332(p) = 1 -\")\n",
    "print((1-r(gcd_pmin1_3=1)).factor().numerator())\n",
    "print(\"/\")\n",
    "print((1-r(gcd_pmin1_3=1)).factor().denominator()); print()\n",
    "print(\"Checking agreement\")\n",
    "print(\"    p = 1 mod 3 (expect 0):\", (r(gcd_pmin1_3=3) - r_332(gcd_pmin1_l_m=3, gcd_pmin1_l_n=1, gcd_pmin1_m_n=1)).factor())\n",
    "print(\"    p = 2 mod 3 (expect 0):\", (r(gcd_pmin1_3=1) - r_332(gcd_pmin1_l_m=1, gcd_pmin1_l_n=1, gcd_pmin1_m_n=1)).factor())\n",
    "print()\n",
    "\n",
    "# p = 3\n",
    "isp3 = true\n",
    "r, rA, rB, rC, rAB, rAC, rBC, r_110_x_val,r_120_x_val,r_101_x_val,r_101_z_val,r_201_z_val,r_011_z_val,r_021_z_val = solve_332(isp3)\n",
    "print(\"p = 3\")\n",
    "print(\"rho_332(3) =\", r(p=3))"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 0,
   "id": "5ecd8d",
   "metadata": {
    "collapsed": false
   },
   "outputs": [
   ],
   "source": [
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "argv": [
    "sage-10.7",
    "--python",
    "-m",
    "sage.repl.ipython_kernel",
    "--matplotlib=inline",
    "-f",
    "{connection_file}"
   ],
   "display_name": "SageMath 10.7",
   "env": {
   },
   "language": "sagemath",
   "metadata": {
    "cocalc": {
     "description": "Open-source mathematical software system",
     "priority": 10,
     "url": "https://www.sagemath.org/"
    }
   },
   "name": "sage-10.7",
   "resource_dir": "/ext/jupyter/kernels/sage-10.7"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.12.5"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}